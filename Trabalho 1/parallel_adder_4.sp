* This file represents the description of a 4 bit parallel adder
* The SPICE list was generated by SwitchCraft.
*.options compat=PSPICE
.include NMOS_VTG.inc
.include PMOS_VTG.inc

.include full_adder_1.sp
.include half_adder.sp

* ------------------------------ *
* Voltage
* Vname n+ n- value
vgnd GND_ 0 
vcc vcc 0 dc 1
vcc2 vcc2 0 dc 1 * for calculating power in carry-out

* ------------------------------ *
.param supply = 1 	* supply == vcc
.param length = '50n'

* ------------------------------ *
* source input
* Vname n+ n- pulse(v1 v2 td trise tfall pw per)
* a = 0111
vin1 a0 0 pwl(0n 1 1n 1 1.0045n 1 2.00n 1 2.0045n 1)
vin2 a1 0 pwl(0n 0 1n 0 1.0045n 1 2.00n 1 2.0045n 0)
vin3 a2 0 pwl(0n 0 1n 0 1.0045n 1 2.00n 1 2.0045n 0)
vin4 a3 0 pwl(0n 1 1n 1 1.0045n 0 2.00n 0 2.0045n 1)

* b = 0100
vin5 b0 0 pwl(0n 1 1n 1 1.0045n 0 2.00n 0 2.0045n 1)
vin6 b1 0 pwl(0n 1 1n 1 1.0045n 0 2.00n 0 2.0045n 1)
vin7 b2 0 pwl(0n 0 1n 0 1.0045n 1 2.00n 1 2.0045n 0)
vin8 b3 0 pwl(0n 1 1n 1 1.0045n 0 2.00n 0 2.0045n 1)

* ------------------------------ *
* Circuit Description
* s0: half-adder
Xh0 a0 b0 s0 vcc hadd_sum
Xc0 a0 b0 c1 vcc2 hadd_carry

* s1: full-adder
Xs1 a1 b1 c1 s1 vcc fadd_sum
Xc1 a1 b1 c1 c2 vcc2 fadd_carry

* s2: full-adder
Xs2 a2 b2 c2 s2 vcc fadd_sum
Xc2 a2 b2 c2 c3 vcc2 fadd_carry

* s3: full-adder
Xs3 a3 b3 c3 s3 vcc fadd_sum
Xc3 a3 b3 c3 c4 vcc2 fadd_carry

* ------------------------------ *
* capacitor
* Cname  n+ n- value
C0 s0 0 1.85fF
C1 s1 0 1.85fF
C2 s2 0 1.85fF
C3 s3 0 1.85fF
CC c4 0 1.85fF

* ------------------------------ *
* transient analysis
* .tran step total
.tran 0.001n 3.5n 

* ------------------------------ *
* measures:
* measure propagation delay
.measure tran pLH_s0 
+ trig v(a3)   val='supply/2' fall=1
+ targ v(s0) val='supply/2' rise=1 

.measure tran pHL_s0 
+ trig v(a3)   val='supply/2' rise=1
+ targ v(s0) val='supply/2' fall=1

.measure tran pLH_s1 
+ trig v(a3)   val='supply/2' fall=1
+ targ v(s1) val='supply/2' rise=1 

.measure tran pHL_s1 
+ trig v(a3)   val='supply/2' rise=1
+ targ v(s1) val='supply/2' fall=1

.measure tran pHL_s2 
+ trig v(a3)   val='supply/2' fall=1
+ targ v(s2) val='supply/2' fall=1 

.measure tran pLH_s2 
+ trig v(a3)   val='supply/2' rise=1
+ targ v(s2) val='supply/2' rise=1

.measure tran pLH_s3 
+ trig v(a3)   val='supply/2' fall=1
+ targ v(s3) val='supply/2' rise=1 

.measure tran pHL_s3 
+ trig v(a3)   val='supply/2' rise=1
+ targ v(s3) val='supply/2' fall=1

.measure tran pHL_c4 
+ trig v(a3)   val='supply/2' fall=1
+ targ v(c4) val='supply/2' fall=1 

.measure tran pLH_c4
+ trig v(a3)   val='supply/2' rise=1
+ targ v(c4) val='supply/2' rise=1

*.measure tran tpd param='(tpdr+tpdf)/2'	* average prop delay (ps)

* ------------------------------ *
* dynamic power
.measure tran power_Som integral 'p(vcc)*(3.5-0)' from=0N to=3.5N
.measure tran power_Carry integral 'p(vcc2)*(3.5-0)' from=0N to=3.5N

.measure tran power_Som_curr integral 'i(vcc)*3.5'  from 0 to 3.5ns
.measure tran power_Carry_curr integral 'i(vcc2)*3.5'  from 0 to 3.5ns

.measure tran avg_power avg power from=0 to=3.5ns 	* average power

* ------------------------------ *
* plot outputs
*.print v(a3) v(a2) v(a1) v(a0)
*.print v(b3) v(b2) v(b1) v(b0)
*.print v(c1) v(c2) v(c3) v(c4)
*.print v(s0) v(s1) v(s2) v(s3)

.end
